module App

import PeerId, Op, Peer from "@fluencelabs/aqua-lib/builtin.aqua"
import AppConfig, UserStatus, TextState from "fluent-pad.aqua"
import User, UserList, AuthResult from "user-list.aqua"
import EmptyServiceResult from "common.aqua"
import History, GetEntriesServiceResult, AddServiceResult from "history.aqua"

export join, getUserList, initAfterJoin, updateOnlineStatuses, leave, auth, getHistory, addEntry, UserStatus, TextState, AppConfig

func join(user: User) -> EmptyServiceResult:
    app <- AppConfig.getApp()
    on app.user_list.peer_id:
        UserList app.user_list.service_id
        res <- UserList.join(user)
    <- res

func getUserList() -> []User:
    app <- AppConfig.getApp()
    on app.user_list.peer_id:
        UserList app.user_list.service_id
        allUsers <- UserList.get_users()
    <- allUsers.users

func initAfterJoin(me: User) -> []User:
    allUsers <- getUserList()
    for user <- allUsers par:
        on user.relay_id:
            isOnline <- Peer.is_connected(user.peer_id)
        if isOnline:
            on user.peer_id via user.relay_id:
                UserStatus.notifyUserAdded(me, true)
        -- else:
        --`    Op.identity("dontcare")
        par UserStatus.notifyUserAdded(user, isOnline)
    <- allUsers


func updateOnlineStatuses():
    allUsers <- getUserList()
    for user <- allUsers par:
        on user.peer_id via user.relay_id:
            isOnline <- Peer.is_connected(user.peer_id)
        UserStatus.notifyOnline(user.peer_id, isOnline)

func leave():
    app <- AppConfig.getApp()
    on app.user_list.peer_id:
        UserList app.user_list.service_id
        res <- UserList.leave(INIT_PEER_ID)
    allUsers <- getUserList()
    for user <- allUsers par:
        on user.peer_id via user.relay_id:
            UserStatus.notifyUserRemoved(INIT_PEER_ID)

func auth() -> AuthResult:
    app <- AppConfig.getApp()
    on app.user_list.peer_id:
        UserList app.user_list.service_id
        res <- UserList.is_authenticated()
    <- res

func getHistory() -> GetEntriesServiceResult:
    app <- AppConfig.getApp()
    authRes <- auth()
    on app.history.peer_id:
        History app.history.service_id
        res <- History.get_all(authRes.is_authenticated)
    <- res

func addEntry(entry: string) -> AddServiceResult:
    app <- AppConfig.getApp()
    authRes <- auth()
    on app.history.peer_id:
        History app.history.service_id
        res <- History.add(entry, authRes.is_authenticated)
    allUsers <- getUserList()
    for user <- allUsers par:
        if user.peer_id != INIT_PEER_ID:
            on user.peer_id via user.relay_id:
                TextState.notifyTextUpdate(entry, authRes.is_authenticated)
    <- res
